group 'io.ruben.minecraft'
version '1.1'

apply plugin: 'scala'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name 'Spigot'
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
    }
    maven {
        name 'BungeeCord'
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        name 'ProtocoLib'
        url 'http://repo.dmulloy2.net/content/groups/public/'
    }
}

sourceSets {
    api {
        scala {
            srcDirs = ['src/api/scala']
            output.classesDir = 'build/classes/api'
        }
    }
    main {
        scala {
            srcDirs = ['src/main/scala', 'src/api/scala']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test {
        scala {
            srcDirs = ['src/test/scala']
        }
    }
}

//compileScala {
//    scalaCompileOptions.useCompileDaemon = true
//    // optionally specify host and port of the daemon:
//    scalaCompileOptions.daemonServer = "localhost:4243"
//}

task copyPlugin(type: Copy, dependsOn: jar) {
    from 'build/libs/main'
    include '**/*.jar'
    into pluginsFolder
}

jar {
    destinationDir = file('build/libs/main')
    from { // use of closure defers evaluation until execution time
        configurations.bundle.collect {
            zipTree(it)
        }
    }
}

task jarAPI(type: Jar, dependsOn: classes) {
    classifier = 'api'
    destinationDir = file('build/libs/api')
    from sourceSets.api.output
}

configurations {
    bundle
}

dependencies {
    compile configurations.bundle
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'org.spigotmc:spigot-api:+'
    compile 'org.scala-lang:scala-library:2.11.7'
    compile 'com.h2database:h2:1.4.188'
    compile 'com.typesafe.slick:slick_2.11:3.0.3'
    compile 'com.comphenix.protocol:ProtocolLib:+'

    apiCompile 'org.spigotmc:spigot-api:+'
    apiCompile 'org.scala-lang:scala-library:2.11.7'
}